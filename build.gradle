import org.jetbrains.dokka.gradle.DokkaTask

/*
 * MIT License
 *
 * Copyright (C) 2020 The SimpleCloud authors
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy of this software and associated
 * documentation files (the "Software"), to deal in the Software without restriction, including without limitation
 * the rights to use, copy, modify, merge, publish, distribute, sublicense, and/or sell copies of the Software,
 * and to permit persons to whom the Software is furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED,
 * INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.
 * IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM,
 * DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE,
 * ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER
 * DEALINGS IN THE SOFTWARE.
 */

plugins {
    id("org.jetbrains.kotlin.jvm") version "2.2.20-Beta1"
    id("org.jetbrains.dokka") version "2.0.0"
    id("com.gradleup.shadow") version "9.0.0-rc1"
}

allprojects {

    group 'eu.thesimplecloud.simplecloud'
    version '2.8.1'

    repositories {
        mavenCentral()
        jcenter()

        maven {
            url 'https://jitpack.io'
        }

        maven {
            url 'https://repository.derklaro.dev/releases/'
        }

        maven {
            url "https://repo.simplecloud.app/releases/"
        }

        maven {
            url "https://libraries.minecraft.net"
        }

        maven {
            name = "papermc"
            url = uri("https://repo.papermc.io/repository/maven-public/")
        }

        maven {
            name 'spongepowered'
            url 'https://repo.spongepowered.org/maven'
        }

        maven {
            url 'https://repo.codemc.io/repository/maven-releases/'
            mavenContent {
                includeGroup("com.github.retrooper.packetevents")
            }
        }

        maven {
            name 'bungeecord-repo'
            url 'https://oss.sonatype.org/content/repositories/snapshots'
        }

        maven {
            name 'spigotmc-repo'
            url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
        }

        // packetevents
        maven {
            url "https://repo.codemc.io/repository/maven-releases/"
            mavenContent {
                includeGroup("com.github.retrooper")
            }
        }
    }

    project.ext {
        depedencyClientServerAPIVersion = '4.1.18'
        dependencyCommonsIOVersion = '2.19.0'
        dependencyKotlinCouroutinesVersion = '1.10.2'
        dependencyNettyVersion = '5.0.0.Alpha2'
        dependencyGuavaVersion = '33.4.8-jre'
        dependencyKMongoVersion = '5.2.1'
    }

    buildscript {
        ext.kotlin_version = '2.2.0'

        repositories {
            mavenLocal()
        }
    }
}

subprojects {

    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'org.jetbrains.kotlin.jvm'
    apply plugin: 'org.jetbrains.dokka'
    apply plugin: 'com.gradleup.shadow'

    sourceCompatibility = JavaVersion.VERSION_23
    targetCompatibility = JavaVersion.VERSION_23

    tasks.register('sourcesJar', Jar) {
        dependsOn classes
        archiveClassifier.set('sources')
        from sourceSets.main.allSource
    }

    dependencies {
        testApi 'org.junit.jupiter:junit-jupiter-api:6.0.0-M1'

        compileOnly "org.jetbrains.kotlin:kotlin-stdlib-jdk8:2.2.20-Beta1"
        testCompileOnly "org.jetbrains.kotlin:kotlin-stdlib-jdk8:2.2.20-Beta1"

        compileOnly(group: 'eu.thesimplecloud.clientserverapi', name: 'clientserverapi', version: depedencyClientServerAPIVersion) {
            transitive = false
        }
        compileOnly(group: 'eu.thesimplecloud.jsonlib', name: 'json-lib', version: "1.0.8") {
            transitive = false
        }

        testApi(group: 'eu.thesimplecloud.clientserverapi', name: 'clientserverapi', version: depedencyClientServerAPIVersion)
        testApi(group: 'eu.thesimplecloud.jsonlib', name: 'json-lib', version: "1.0.8")

        compileOnly group: 'commons-io', name: 'commons-io', version: dependencyCommonsIOVersion
        compileOnly group: 'com.google.guava', name: 'guava', version: dependencyGuavaVersion
        compileOnly group: 'io.netty', name: 'netty-all', version: dependencyNettyVersion
        compileOnly group: 'org.jetbrains.kotlinx', name: 'kotlinx-coroutines-core', version: dependencyKotlinCouroutinesVersion
        compileOnly group: 'com.google.code.gson', name: 'gson', version: '2.13.1'

    }

    compileKotlin {
        kotlinOptions.jvmTarget = "23"
    }

    compileTestKotlin {
        kotlinOptions.jvmTarget = "23"
    }

    jar {
        manifest {
            attributes(
                    'Implementation-Version': project.version,
            )
        }
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                artifact sourcesJar
            }
        }
        repositories {
            maven {
                if (project.version.endsWith("SNAPSHOT")) {
                    url 'https://repo.simplecloud.app/snapshots/'
                } else {
                    url 'https://repo.simplecloud.app/releases/'
                }

                credentials {
                    username = project.hasProperty("mavenUser") ? project.property("mavenUser") : System.getenv("MAVEN_USERNAME")
                    password = project.hasProperty("mavenPassword") ? project.property("mavenPassword") : System.getenv("MAVEN_PASSWORD")
                }
            }
        }
    }

    tasks.withType(DokkaTask).configureEach {
        outputDirectory.set(file("$buildDir/dokka"))
    }
}
